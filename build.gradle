plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'

	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_20
targetCompatibility = JavaVersion.VERSION_20

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def ENV = System.getenv()

repositories {
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.architectury.dev" }
	maven { url 'https://maven.nucleoid.xyz' }
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url 'https://maven.terraformersmc.com/releases' }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	mappings loom.layered {
		//it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${rootProject.minecraft_version}+build.${rootProject.quilt_mappings}:v2"))
		// https://lambdaurora.dev/tools/import_quilt.html
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// LuckPerms
	modImplementation(include('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))

	modImplementation(include("xyz.nucleoid:server-translations-api:${project.translations_version}"))
	//modImplementation("com.github.Globox1997:RpgDifficulty:${project.rpg_difficulty_version}")

	// Allows in game config editing
	modImplementation(include("com.github.samolego.Config2Brigadier:config2brigadier-fabric:${project.c2b_version}"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	it.options.release = 21
}


java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		mavenLocal()
	}
}

// from FAPI https://github.com/FabricMC/fabric/blob/1.16/build.gradle
curseforge {
	if (ENV.CURSEFORGE_API_KEY) {
		apiKey = ENV.CURSEFORGE_API_KEY
	}

	project {
		id = "466750"
		changelog = "A changelog can be found at https://github.com/samolego/HealthCare/releases/latest"
		releaseType = "release"
		addGameVersion "${project.minecraft_version}"
		addGameVersion "Fabric"
		addGameVersion "Quilt"

		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
			displayName = "[${project.minecraft_version}] Healthcare ${version}"
		}

		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}

	options {
		forgeGradleIntegration = false
	}
}

modrinth {
	token = ENV.MODRINTH_TOKEN
	projectId = "NNqujQWr"
	versionNumber = version
	versionName = "[${project.minecraft_version}] Healthcare ${version}"
	versionType = "release"

	uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")

	gameVersions = ["${project.minecraft_version}"]
	loaders = ['fabric', 'quilt']

	dependencies { // A special DSL for creating dependencies
		required.project "fabric-api" // Creates a new required dependency on Fabric API
	}
}
